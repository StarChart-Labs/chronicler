buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
            url 'http://jcenter.bintray.com'
            mavenCentral()
        }
    }
    dependencies {
        classpath group: 'com.netflix.nebula', name: 'nebula-dependency-recommender', version: '3.6.3'
        classpath group: 'com.blackducksoftware.integration', name: 'hub-gradle-plugin', version: '4.0.0'
    }
}

plugins {
    id 'org.sonarqube' version '2.3'
}

apply plugin: 'com.blackducksoftware.hub'

if (project.hasProperty('sonarLogin')) {
    System.setProperty('sonar.login', "${sonarLogin}")
}

sonarqube {
    properties {
        property 'sonar.projectName', 'Lockdown'
        property 'sonar.projectKey', 'com.coronaide:chronicler'
        property 'sonar.organization', 'corona-ide'
        property 'sonar.junit.reportsPath', file("${buildDir}/test-results/test")
        property 'sonar.core.codeCoveragePlugin', 'jacoco'
        property 'sonar.jacoco.reportPaths', "${buildDir}/jacoco/test.exec"
        property 'sonar.host.url', 'https://sonarqube.com'
    }
}

buildBom{
	deployHubBdio false
}

allprojects{
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'jacoco'
    apply plugin: 'nebula.dependency-recommender'

    dependencyRecommendations {
        dependencyLock file: file("${rootDir}/dependencies.lock")
    }

    version = "${chroniclerVersion}"
    group = 'com.coronaide.chronicler'

    sourceCompatibility = "${javaVersion}"

    //Always download sources, to allow debugging, and use Eclipse containers for greater portability
    eclipse {
        classpath {
            downloadSources=true
            containers = [ "org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-${sourceCompatibility}".toString() ]
        }
    }

    repositories {
        mavenCentral()
    }

    //Task which will show what the dependency set of the project is in a tree form
    task dependencyReport(type: DependencyReportTask) {}

    //Task which will show what is introducing a particular dependency
    task dependencyInsightReport(type: DependencyInsightReportTask) {}

}

subprojects{
    apply plugin: 'maven'

    //All projects should provide source code and javadoc, and upload these with any released artifacts
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    //Setup default test behavior, including failure logging
    test {
        testLogging {
            exceptionFormat 'full'

            quiet {
                events 'failed'
            }

            info {
                events 'skipped', 'failed', 'passed'
            }

            debug {
                events 'started', 'standard_out', 'standard_error', 'skipped', 'failed', 'passed'
            }
        }

        useTestNG() {
            useDefaultListeners = true
        }
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    sonarqube {
        properties {
            property 'sonar.projectKey', "com.coronaide:chronicler:${project.name}"
            property 'sonar.junit.reportsPath', file("${buildDir}/test-results/test")
            property 'sonar.core.codeCoveragePlugin', 'jacoco'
            property 'sonar.jacoco.reportPaths', "${buildDir}/jacoco/test.exec"
            properties['sonar.sources'] = sourceSets.main.java.srcDirs.findAll { it.exists() && !it.path.contains('resources') } ?: []
        }
    }
}

//Create an aggregated jacoco report
task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        xml.destination "${buildDir}/reports/jacoco/report.xml"
        html.enabled false
        csv.enabled false
    }
}

codeCoverageReport.dependsOn {
    subprojects*.test
}

task wrapper(type: Wrapper) {
    gradleVersion = "${gradleVersion}"
}
